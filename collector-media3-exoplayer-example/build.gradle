apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    namespace 'com.bitmovin.analytics.media3.exoplayer.example'
    compileSdk rootProject.ext.compileSdk

    defaultConfig {
        applicationId "com.bitmovin.analytics.media3.exoplayer.example"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    buildFeatures{
        viewBinding = true
    }

    compileOptions {
        sourceCompatibility rootProject.ext.sourceCompatibility
        targetCompatibility rootProject.ext.targetCompatibility
    }

    kotlinOptions {
        jvmTarget = rootProject.ext.exampleAppJvmTarget
    }

    buildTypes {
        debug {
            // testing proguard rules requires to create a release of the core collector
            // (in order to apply the consumer pro guard rules)
            // and then reference the core collector in the player specific library
            // and setting minifyEnable to true here
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lint {
        abortOnError false
    }
}

dependencies {
    def testVersion = rootProject.ext.media3PlayerVersion
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${rootProject.ext.kotlin_version}"
    implementation "androidx.appcompat:appcompat:${rootProject.ext.appCompatVersion}"
    implementation "androidx.constraintlayout:constraintlayout:${rootProject.ext.constraintlayoutVersion}"
    implementation "androidx.work:work-runtime-ktx:${rootProject.ext.workRuntimeKtxVersion}"
    implementation "androidx.multidex:multidex:${rootProject.ext.multidexVersion}"

    implementation "androidx.media3:media3-exoplayer:${testVersion}"
    implementation "androidx.media3:media3-exoplayer-dash:${testVersion}"
    implementation "androidx.media3:media3-exoplayer-hls:${testVersion}"
    implementation "androidx.media3:media3-ui:${testVersion}"

    if (rootProject.ext.developLocal) {
        implementation project(':collector-media3-exoplayer')
    } else {
        implementation "com.bitmovin.analytics:collector-media3-exoplayer:${rootProject.ext.version}"
    }
    implementation project(':example-shared')
}
