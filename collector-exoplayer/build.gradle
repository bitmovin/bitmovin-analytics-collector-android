apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'
apply from: '../bitmovinpropertiesloader.gradle'

android {
    compileSdkVersion project.compileSdkVersion

    defaultConfig {
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
        versionCode project.versionCode
        versionName project.version

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility project.sourceCompatibility
        targetCompatibility project.targetCompatibility
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${project.kotlin_version}"
    implementation "com.android.support:appcompat-v7:${project.compileSdkVersion}.1.1"
    testImplementation "junit:junit:${project.junitVersion}"
    androidTestImplementation "com.android.support.test:runner:${project.testRunnerVersion}"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:${project.espressoCoreVersion}"


    testImplementation "org.assertj:assertj-core:${project.assertjCoreVersion}"
    androidTestImplementation "org.assertj:assertj-core:${project.assertjCoreVersion}"
    testImplementation "org.mockito:mockito-core:${project.mockitoCoreVersion}"
    androidTestImplementation "org.mockito:mockito-android:${project.mockitoCoreVersion}"

    if (project.developLocal) {
        api project(':collector')
    } else {
        api "com.bitmovin.analytics:collector:${project.version}"
    }
    implementation "com.google.android.exoplayer:exoplayer:${project.exoplayerVersion}"
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId project.packageGroupId
            version = project.version
            artifactId project.getName()

            // Tell maven to prepare the generated "*.aar" file for publishing
            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Defining configuration names from which dependencies will be taken (debugCompile or releaseCompile and compile)
                def configurationNames = ['api']

                configurationNames.each { configurationName ->
                    configurations[configurationName].allDependencies.each {

                        if ((it.group == 'com.google.android.libraries.cast.companionlibrary' && it.name == 'ccl') ||
                                (it.group == 'com.android.support' && it.name == 'mediarouter-v7') ||
                                (it.group == 'com.android.support' && it.name == 'support-v4') ||
                                (it.group == 'com.android.support' && it.name == 'appcompat-v7') ||
                                (it.group == 'com.google.android.gms' && it.name == 'play-services-cast-framework') ||
                                (it.group == 'com.google.ads.interactivemedia.v3' && it.name == 'interactivemedia') ||
                                (it.group == 'com.google.android.gms' && it.name == 'play-services-ads')) {
                            return
                        }

                        if (it.group == null || it.name == null) {
                            return
                        }

                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

artifactory {
    contextUrl = 'https://bitmovin.jfrog.io/bitmovin'
    publish {
        repository {
            // The Artifactory repository key to publish to
            repoKey = project.version.endsWith('SNAPSHOT') ? 'libs-snapshot-local' : 'libs-release-local'
            username = bitmovinProperties.getProperty('artifactoryUser') // The publisher user name
            password = bitmovinProperties.getProperty('artifactoryPassword')
            // The publisher password
            maven = true
        }
        defaults {
            // Tell the Artifactory Plugin which artifacts should be published to Artifactory.
            publications('aar')
            publishArtifacts = true

            // Properties to be attached to the published artifacts.
            properties = ['qa.level': 'basic', 'dev.team': 'core']
            // Publish generated POM files to Artifactory (true by default)
            publishPom = true
        }
    }
}
