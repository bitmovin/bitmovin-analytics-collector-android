buildscript {
    ext.kotlin_version = '1.8.20'

    repositories {
        google()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'https://artifacts.bitmovin.com/artifactory/public-releases' }
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath "com.diffplug.spotless:spotless-plugin-gradle:6.13.0"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.31.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath 'org.jacoco:org.jacoco.core:0.8.8'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id 'java'
    id 'jacoco'
    id "org.sonarqube" version "3.5.0.2730"
    id 'org.jetbrains.kotlinx.binary-compatibility-validator' version '0.12.1'
}

apply from: './bitmovinpropertiesloader.gradle'

allprojects {
    repositories {
        mavenLocal()
        google()
        maven { url 'https://artifacts.bitmovin.com/artifactory/public-releases' }
        maven {
            credentials {
                username bitmovinProperties.getProperty('artifactoryUser')
                password bitmovinProperties.getProperty('artifactoryPassword')
            }
            url 'https://bitmovin.jfrog.io/bitmovin/libs-snapshot-local/'
        }
        mavenCentral()
    }

    apply plugin: "com.diffplug.spotless"

    spotless {
        // only run on modified files
        ratchetFrom 'origin/main'

        format 'misc', {
            // define the files to apply `misc` to
            target '**/*.gradle', '**/*.md', '**/*.gitignore'

            // define the steps to apply to those files
            trimTrailingWhitespace()
            indentWithSpaces()
            endWithNewline()
        }
        kotlin {
            target '**/*.kt'
            ktlint()
        }
        java {
            // apply a specific flavor of google-java-format
            googleJavaFormat('1.8').aosp().reflowLongStrings()
            // fix formatting of type annotations
            formatAnnotations()
        }
    }
}

ext {
    developLocal = System.getProperty("developLocal", "true").toBoolean()
    packageGroupId = 'com.bitmovin.analytics'

    /**
     * Not relevant for us as versionCode is used only by PlayStore to indicate updates of apps,
     * but we publish artifacts to maven repositories
     */
    versionCode = 122003

    version = System.getProperty("version", "0.0.0") + (developLocal ? '-local' : '')

    compileSdkVersion = 33
    minSdkVersion = 16
    amazonIvsMinSdkVersion = 21
    targetSdkVersion = 33

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    jvmTarget = JavaVersion.VERSION_1_8.toString()

    // player versions
    bitmovinPlayerVersion = '3.39.0'
    exoplayerVersion = '2.18.7'
    amazonIvsPlayerVersion = '1.19.0'

    // collector dependencies
    gsonVersion = '2.8.9'
    okhttp3Version = '4.10.0'
    appCompatVersion = '1.6.1' // required for bitmovin player and @Keep annotation in core collector to avoid proguard issues
    coroutinesVersion = "1.6.1"

    // example apps dependencies
    playServicesAdsVersion = '19.8.0'
    imaSdkVersion = '3.28.2'
    constraintlayoutVersion = '2.1.3'
    multidexVersion = '2.0.1'

    // test dependencies
    testRunnerVersion = '1.5.2'
    mockitoCoreVersion = '5.1.1'
    junitVersion = '4.13.2'
    junitXVersion = '1.1.5'
    mockkVersion = '1.13.4'
    assertjCoreVersion = '3.24.2'
    robolectricVersion = '4.9'
    testCoreXVersion = '1.5.0'
}

task createPrePushHook() {
    def gitDirStream = new ByteArrayOutputStream()
    exec {
        commandLine "git", "rev-parse", "--git-dir"
        workingDir = project.rootDir
        standardOutput = gitDirStream
    }
    def gitDir = project.file(gitDirStream.toString().trim())
    def gitHooksDirectory = new File(gitDir, "/hooks/")
    if (!gitHooksDirectory.exists()) gitHooksDirectory.mkdirs()
    def prePushHookFile = new File(gitDir, "/hooks/pre-push")
    if (prePushHookFile.exists()) prePushHookFile.delete()
    prePushHookFile.text = """
    #!/bin/sh

    echo '[git hook] executing gradle spotlessCheck before push'

    oldsha=\$(git rev-parse -q --verify refs/stash)
    # stash any unstaged changes
    git stash save --keep-index --include-untracked
    newsha=\$(git rev-parse -q --verify refs/stash)

    # run the spotlessCheck with the gradle wrapper
    ./gradlew spotlessCheck --daemon

    # store the last exit code in a variable
    RESULT=\$?

    # unstash the unstaged changes
    if [ "\$oldsha" != "\$newsha" ];
    then
        echo 'Popping stash as there have been changes before.'
        git stash pop;
    fi

    # return the './gradlew spotlessCheck' exit code
    exit \$RESULT
"""
    "chmod +x .git/hooks/pre-push".execute()
}

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "bitmovin-engineering"
        property "sonar.projectName", "Analytics Collector Android"
        property "sonar.projectKey", "bitmovin-engineering_bitmovin-analytics-collector-android-internal"
        property "sonar.tests", ["src/test/java"]
        property "sonar.test.inclusions", "**/*Test*/**"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.sources", "src/main/java"
        property "sonar.exclusions", '**/*Test*/**,' +
                '*.json,' +
                '**/*test*/**,' +
                '**/.gradle/**,' +
                '**/R.class'
    }
}

// config for org.jetbrains.kotlinx.binary-compatibility-validator
// this plugin is used to ensure binary compatibility on our public APIs between versions
// it needs to be run manually for now since collector has too many public methods
// ./gradlew apiCheck #runs the check against the current dump (stored in /api/ for each module)
// ./gradlew apiDump #creates a new dump which we can run against in the future
apiValidation {
    /**
     * Sub-projects that are excluded from API validation
     */
    ignoredProjects += ["collector-bitmovin-player-example",
                        "collector-bitmovin-player-tv-example",
                        "collector-exoplayer-example",
                        "collector-amazon-ivs-example",
                        "collector",    // excluded until we have a better structure with internal package
                        "example-shared",
                        "systemtest-utils"]
}

project(":collector-exoplayer-example") {
    sonarqube {
        sonar.skipProject = true
    }
}
project(":collector-bitmovin-player-example") {
    sonarqube {
        sonar.skipProject = true
    }
}
project(":collector-bitmovin-player-tv-example") {
    sonarqube {
        sonar.skipProject = true
    }
}
project(":collector-amazon-ivs-example") {
    sonarqube {
        sonar.skipProject = true
    }
}

project(":systemtest-utils") {
    sonarqube {
        sonar.skipProject = true
    }
}

java {
    sourceCompatibility = rootProject.ext.sourceCompatibility
    targetCompatibility = rootProject.ext.targetCompatibility
}
