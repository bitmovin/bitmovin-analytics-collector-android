buildscript {
	ext.kotlin_version = '1.7.20'

	repositories {
		google()
		maven { url "https://plugins.gradle.org/m2/" }
		maven { url 'https://artifacts.bitmovin.com/artifactory/public-releases' }
		mavenCentral()
	}

	dependencies {
		classpath 'com.android.tools.build:gradle:7.4.0'
		classpath "com.diffplug.spotless:spotless-plugin-gradle:6.13.0"
		classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.31.0"
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
		classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
		classpath 'org.jacoco:org.jacoco.core:0.8.8'

		classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.3"
		// NOTE: Do not place your application dependencies here; they belong
		// in the individual module build.gradle files
	}
}

plugins {
	id 'java'
	id 'jacoco'
	id "org.sonarqube" version "3.3"
}

apply from: './bitmovinpropertiesloader.gradle'

allprojects {
	repositories {
		mavenLocal()
		google()
		maven { url 'https://artifacts.bitmovin.com/artifactory/public-releases' }
		maven {
			credentials {
				username bitmovinProperties.getProperty('artifactoryUser')
				password bitmovinProperties.getProperty('artifactoryPassword')
			}
			url 'https://bitmovin.jfrog.io/bitmovin/libs-snapshot-local/'
		}
		mavenCentral()
	}

	apply plugin: "com.diffplug.spotless"

	spotless {
		// optional: limit format enforcement to just the files changed by this feature branch
		ratchetFrom 'origin/main'

		format 'misc', {
			// define the files to apply `misc` to
			target '*.gradle', '*.md', '.gitignore'

			// define the steps to apply to those files
			trimTrailingWhitespace()
			indentWithTabs() // or spaces. Takes an integer argument if you don't like 4
			endWithNewline()
		}
		groovyGradle {
			target '**/*.gradle' // default target of groovyGradle
			greclipse()
		}
		kotlin {
			target '**/*.kt'
			ktlint()
		}
		java {
			// apply a specific flavor of google-java-format
			googleJavaFormat('1.8').aosp().reflowLongStrings()
			// fix formatting of type annotations
			formatAnnotations()
			// make sure every file has the following copyright header.
			// optionally, Spotless can set copyright years by digging
			// through git history (see "license" section below)
			licenseHeader '/* (C)$YEAR */'
		}
	}

}

ext {
	developLocal = System.getProperty("developLocal", "true").toBoolean()
	packageGroupId = 'com.bitmovin.analytics'

	/**
	 * Not relevant for us as versionCode is used only by PlayStore to indicate updates of apps,
	 * but we publish artifacts to maven repositories
	 */
	versionCode = 122003

	version = System.getProperty("version", "0.0.0") + (developLocal ? '-local' : '')

	compileSdkVersion = 33
	minSdkVersion = 16
	targetSdkVersion = 33

	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
	jvmTarget = JavaVersion.VERSION_1_8.toString()

	//	player versions
	bitmovinPlayerVersion = '3.25.1'
	exoplayerVersion = '2.18.1'

	// dependencies
	appCompatVersion = '1.1.0'
	constraintlayoutVersion = '2.1.3'
	multidexVersion = '2.0.1'
	imaSdkVersion = '3.28.2'
	playServicesAdsVersion = '19.7.0'

	// for tests
	testRunnerVersion = '1.0.1'
	mockitoCoreVersion = '3.5.0'
	junitVersion = '4.13.2'
	junitXVersion = '1.1.5'
	mockkVersion = '1.10.6'
	assertjCoreVersion = '3.11.1'
}

task createPrePushHook() {
	def gitHooksDirectory = new File("$project.rootDir/.git/hooks/")
	if (!gitHooksDirectory.exists()) gitHooksDirectory.mkdirs()
	def prePushHookFile = new File("$project.rootDir/.git/hooks", "pre-push")
	if (prePushHookFile.exists()) prePushHookFile.delete()
	prePushHookFile.text = """
	#!/bin/sh

	echo '[git hook] executing gradle spotlessCheck before push'

	oldsha=\$(git rev-parse -q --verify refs/stash)
	# stash any unstaged changes
	git stash save --keep-index --include-untracked
	newsha=\$(git rev-parse -q --verify refs/stash)

	# run the spotlessCheck with the gradle wrapper
	./gradlew spotlessCheck --daemon

	# store the last exit code in a variable
	RESULT=\$?

	# unstash the unstaged changes
	if [ "\$oldsha" != "\$newsha" ];
	then
		echo 'Popping stash as there have been changes before.'
		git stash pop;
	fi

	# return the './gradlew spotlessCheck' exit code
	exit \$RESULT
"""
	"chmod +x .git/hooks/pre-push".execute()
}

sonarqube {
	properties {
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.organization", "bitmovin-engineering"
		property "sonar.projectName", "Analytics Collector Android"
		property "sonar.projectKey", "bitmovin-engineering_bitmovin-analytics-collector-android-internal"
		property "sonar.tests", ["src/test/java"]
		property "sonar.test.inclusions", "**/*Test*/**"
		property "sonar.sourceEncoding", "UTF-8"
		property "sonar.sources", "src/main/java"
		property "sonar.exclusions", '**/*Test*/**,' +
				'*.json,' +
				'**/*test*/**,' +
				'**/.gradle/**,' +
				'**/R.class'
	}
}

project(":collector-exoplayer-example") {
	sonarqube {
		skipProject = true
	}
}
project(":collector-bitmovin-player-example") {
	sonarqube {
		skipProject = true
	}
}
project(":collector-bitmovin-player-tv-example") {
	sonarqube {
		skipProject = true
	}
}
project(":collector-amazon-ivs-example") {
	sonarqube {
		skipProject = true
	}
}

java {
	sourceCompatibility = rootProject.ext.sourceCompatibility
	targetCompatibility = rootProject.ext.targetCompatibility
}
