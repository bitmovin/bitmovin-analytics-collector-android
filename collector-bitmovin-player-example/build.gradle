apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    namespace "com.bitmovin.analytics.bitmovinplayer.example"
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId 'com.bitmovin.analytics.bitmovinplayer.example'
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }


    compileOptions {
        sourceCompatibility rootProject.ext.sourceCompatibility
        targetCompatibility rootProject.ext.targetCompatibility
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures{
        viewBinding = true
    }
    lint {
        abortOnError false
    }
}

// we need to exclude the bitmovin player analytics module since we work with a local development version
// otherwise we would run into dex errors due to duplicate classes
configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute module('com.bitmovin.analytics:collector') using project(':collector') because "we work with local development version and need to exclude bundled analytics to avoid conflicts"
        substitute module('com.bitmovin.analytics:collector-bitmovin-player') using project(':collector-bitmovin-player') because "we work with local development version and need to exclude bundled analytics to avoid conflicts"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${rootProject.ext.kotlin_version}"
    implementation "androidx.appcompat:appcompat:${rootProject.ext.appCompatVersion}"
    implementation "androidx.constraintlayout:constraintlayout:${rootProject.ext.constraintlayoutVersion}"
    implementation "androidx.multidex:multidex:${rootProject.ext.multidexVersion}"
    implementation 'androidx.work:work-runtime-ktx:2.7.1'

    implementation "com.bitmovin.player:player:${rootProject.ext.bitmovinPlayerVersion}"

    // dependencies for ads
    implementation "com.google.ads.interactivemedia.v3:interactivemedia:${rootProject.ext.imaSdkVersion}" // only needed if ads are used
    implementation "com.google.android.gms:play-services-ads:${rootProject.ext.playServicesAdsVersion}" // only needed if ads are used

    // dependencies for chromecast
    implementation "com.google.android.gms:play-services-cast-framework:19.0.0" //only needed if Chromecast is used
    implementation 'androidx.mediarouter:mediarouter:1.0.0'  //only needed if Chromecast is used

    // dependencies for ads and Chromecast
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    if (rootProject.ext.developLocal) {
        implementation project(':collector-bitmovin-player')
    } else {
        implementation "com.bitmovin.analytics:collector-bitmovin-player:${rootProject.ext.version}"
    }
    implementation project(':example-shared')
}
